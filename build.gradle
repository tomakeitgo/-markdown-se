apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.tomakeitgo.markdown'
archivesBaseName = "markdown-se"
version = '1.0.1'


repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'markdown-se'
                packaging 'jar'

                description 'A markdown based on lisp syntax'
                url 'https://github.com/tomakeitgo/markdown-se'

                scm {
                    url 'https://github.com/tomakeitgo/markdown-se'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'will.lowery'
                        name 'Will Lowery'
                        email 'will.lowery@tomakeitgo.com'
                    }
                }
            }
        }
    }
}